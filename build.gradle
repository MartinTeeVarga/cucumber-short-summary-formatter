import org.apache.tools.ant.filters.ReplaceTokens

import java.time.LocalDateTime
import java.time.format.DateTimeFormatter
import java.time.format.FormatStyle

plugins {
    id "com.jfrog.bintray" version "1.7.3"
    id 'net.researchgate.release' version '2.5.0'
}
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

repositories {
    jcenter()
}

dependencies {
    compile 'info.cukes:gherkin:2.12.2'
    compile 'info.cukes:gherkin-jvm-deps:1.0.3'
    testCompile 'junit:junit:4.11'
    testCompile 'info.cukes:cucumber-java8:1.2.5'
    testCompile 'info.cukes:cucumber-junit:1.2.5'
}

sourceSets {
    test {
        java.srcDir 'src/test/unit/java'
        resources.srcDir 'src/test/unit/resources'
    }
    cuke {
        java.srcDir 'src/test/cuke/java'
        resources.srcDir 'src/test/cuke/resources'
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    cukeCompile.extendsFrom testCompile
    cukeRuntime.extendsFrom testRuntime
}

processTestResources {
    DateTimeFormatter dateTimeFormatter = DateTimeFormatter.ofLocalizedDate(FormatStyle.MEDIUM);
    String now = dateTimeFormatter.format(LocalDateTime.now());
    filter ReplaceTokens, tokens: [now: now]
}

task cuke(type: Test, description: 'Runs the mock cuke tests to produce report.', group: 'Verification') {
    testClassesDir = sourceSets.cuke.output.classesDir
    classpath = sourceSets.cuke.runtimeClasspath
}
test.dependsOn cuke

publishing {
    publications {
        publication(MavenPublication) {
            from components.java
            groupId 'com.czequered.cucumber'
            artifactId 'short-summary-formatter'
        }
    }
}

bintray {
    user = bintray_user     // saved in $GRADLE_HOME/gradle.properties
    key = bintray_api_key   // saved in $GRADLE_HOME/gradle.properties
    pkg {
        repo = 'maven'
        name = 'cucumber-short-summary-formatter'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/sm4/cucumber-short-summary-formatter.git'
    }
}

afterReleaseBuild.dependsOn bintray

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}